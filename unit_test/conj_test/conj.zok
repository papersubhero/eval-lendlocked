from "ecc/babyjubjubParams" import BabyJubJubParams;
import "signatures/verifyEddsa.code" as verifyEddsa;
import "ecc/babyjubjubParams.code" as context;

import "hashes/sha256/512bitPadded" as sha256;

// leave the root out of the struct as all the variables 
// in the struct are all private and the root is public
struct MerkleTreeProofStruct<DEPTH> {
	u32[8] leaf;
	bool[DEPTH] directionSelector; 
	u32[DEPTH][8] path;
}

// directionSelector => true if current digest is on the rhs of the hash
def select(bool condition, u32[8] left, u32[8] right) -> (u32[8], u32[8]) {
	return (condition ? right : left, condition ? left : right);
}

// Merkle-Tree inclusion proof for tree depth <DEPTH> using sha256
def merkleTreeProof<DEPTH>(u32[8] root, MerkleTreeProofStruct<DEPTH> proof) -> bool {
    // Start from the leaf
    u32[8] mut digest = proof.leaf;

	// Loop up the tree
	for u32 i in 0..DEPTH {
		(u32[8], u32[8]) s = select(proof.directionSelector[i], digest, proof.path[i]);
		digest = sha256(s.0, s.1);
	}

    return digest == root;
}

const u32 TREE_DEPTH = 3;

// put merkle tree witness after the witness of signature
def main(private field[2] R, private field S, field[2] A, u32[8] M0, u32[8] M1, u32[8] treeRoot, private MerkleTreeProofStruct<TREE_DEPTH> proof){
  // context necessary for the signature verification
  BabyJubJubParams context = context();
  //Verify Signature (R,S) with PupKey (A) on Hash (M0, M1);
  bool isVerified = verifyEddsa(R, S, A, M0, M1, context);
  // both conditions must be true
  assert(isVerified == true);
  assert(merkleTreeProof(treeRoot, proof));
  return;
}